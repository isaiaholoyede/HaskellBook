# Chapter 16: Functor

## Introduction

This is my notes from **Chapter 16: Functor** of the book [Haskell Programming From First Principles](http://haskellbook.com).

## 16.1 Functor

Functor is all about a pattern of mapping over structure.

## 16.2 What's a Functor?

A functor is a way to apply a function over or around some structure that we don't want to alter. We want to apply the function to the value that is "inside" some structure and leave the structure alone.

```haskell
class Functor f where
  fmap :: (a -> b) -> f a -> f b
```

## 16.3 There's a whole lot of `fmap` goin' round

We can apply `fmap` to lists, tuples, Either, etc.

## 16.4 Let's talk about `f`, baby

The `f` in the typeclass definition for `Functor` must be the same `f` throughout the entire definition. The `f` must refer to a type that implements the functor typeclass. The `f` must have the kind `* -> *`. A type with kind `* -> *` is awaiting application to a type constant of kind `*`.

Each argument (and result) in the type signature for a function must be a fully applied and inhabitable type. Each argument must have the kind `*`. Since `f a` and `f b` must each have the kind `*`, `f` by itself must be kind `* -> *`.

Every argument to the type constructor `->` must be of kind `*`. Each argument and result of every function must be a type constant, not a type constructor.

```haskell
Prelude> :k (->)
(->) :: * -> * -> *
```

`GHC` has kind inference as well as type inference.

`<$>` is the infix alias for `fmap`.

`Functor` is a typeclass for function application "over", or "through", some structure `f` that we want to ignore and leave untouched.

## 16.5 Functor Laws

Instances of the `Functor` typeclass should abide by two basic laws.

  1. The first law is the law of identity. `fmap id == id`
  2. The second law is the law of composition.

      ```haskell
      fmap (f . g) == fmap f . fmap g
      ```

## 16.6 The Good, the Bad, and the Ugly

Our `Functor` instance must follow the identity law or else it is not a valid functor.

Application of a functor changes the value, but must leave the structure untouched. If you want a function that can change both the value and structure, you use the plain old function.

A valid functor must follow the composition law as well.

## 16.7 Commonly used functors

You can apply functors over:

- Maybe datatype `data Maybe a = Nothing | Just a`
- List `data [] a = [] | a : [a]`
- Tuple `data (,) a b = (,) a b`

You can apply functors over functions.

## 16.8 Transforming the unapplied type argument

The goal of `fmapping` is to leave the outer structure untouched while transforming the type arguments inside.

When we `fmap` over a tuple, it only transforms the second argument (the `b`) because tuple has a kind `* -> * -> *`, but functor applies to a kind `* -> *`, so we need to partially apply the tuple to transform it from `* -> * -> *` to `* -> *`.

```haskell
data Two a b = Two a b deriving (Eq, Show)

data Or a b = First a | Second b deriving (Eq, Show)

instance Functor (Two a) where
  fmap f (Two a b) = Two a (f b)

instance Functor (Or a) where
  fmap _ (First a) = First a
  fmap f (Second b) = Second (f b)
```

## 16.9 QuickChecking Functor Instances

The functor laws are:

```haskell
fmap id = id
fmap (p . q) = (fmap p) . (fmap q)
```

The two laws turned into QuickCheck properties are:

```haskell
functorIdentity :: (Functor f, Eq (f a)) => f a -> Bool
functorIdentity f = fmap id f == f

functorCompose :: (Eq (f c), Functor f) =>
                      (a -> b)
                   -> (b -> c)
                   -> f a
                   -> Bool
functorCompose f g x =
  (fmap g (fmap f x)) == (fmap (g . f) x)   

```

## 16.11 Ignoring Possibilities

The `Functor` instances for `Maybe` and `Either` functors are useful for times we intend to ignore the left cases, which are typically error or failure cases

## 16.12 A Somewhat Surprising Functor

`Constant` is a lawful Functor.

```haskell
newtype Constant a b =
  Constant {getConstant :: a}
  deriving (Eq, Show)

instance Functor (Constant m) where
  fmap _ (Constant v) = Constant v

```

## 16.13 More Structure, More Functors

Sometimes the structure of our types may require that we also have a Functor instance for an intermediate type layer.

```haskell
data Wrap f a = Wrap (f a) deriving (Eq, Show)

instance Functor f => Functor (Wrap f) where
  fmap f (Wrap fa) = Wrap (fmap f fa)
```

## 16.14 IO Functor

The IO type is an abstract datatype. The IO type has no data constructors we can pattern match on. The typeclasses IO provides are the only way we can work with values of type `IO a`. One of the typeclasses IO provides is `Functor`.

## 16.15 What if we want to do something different?

If we want to transform only the structure and leave the type argument to that structure or type constructor alone, we have `natural transformations`.

```haskell
{-# LANGUAGE RankNTypes #-}

type Nat f g = forall a . f a -> g a
```

If we want to transform the values, we should write a plain old fold.

## 16.16 Functors are unique to a datatype

In Haskell, `Functor` instances will be unique for a given datatype.

## References

1. [Haskell Programming from First Principles](http://haskellbook.com/)