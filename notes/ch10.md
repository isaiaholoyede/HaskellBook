# Chapter 10: Folding Lists - Notes

## Introduction

This is my notes from **Chapter 10: Folding Lists** of the book [Haskell Programming From First Principles](http://haskellbook.com).

## 10.1 Folds

Folds are called catamorphisms. Catamorphisms are used to deconstruct data.

## 10.2 Bringing you into the fold

A fold replaces the cons constructor of a list with a function and reduces the list. Where map applies a function to each member of a list and returns a list, a fold replaces the cons constructors with the function and reduces the list.

## 10.3 Recursive patterns

The base case for the recursive function is the identity for that function. For example, the identity for sum is 0.

```Haskell
-- Find the sum of all elements in a list
sum :: [Integer] -> Integer
sum []     = 0
sum (x:xs) = x + sum xs

-- Find the length (or number of the elements) of a list
length :: [a] -> Integer
length []     = 0
length (_:xs) = 1 + length xs

-- Find the product of all elements in a list
product :: [Integer] -> Integer
product []     = 1
product (x:xs) = x * product xs

-- concatenate the elements of a list
concat :: [[a]] -> [a]
concat []     = []
concat (x:xs) = x ++ concat xs

-- convert to string all elements of a list
toString :: [Int] -> String
toString [] = ""
toString (x:xs) = show x ++ toString xs

-- Does all the elements in a list satisfy a property?
all :: (a -> Bool) -> [a] -> Bool
all _ [] = True
all p (x:xs) = p x && all p xs
```

They have similar structural pattern. We have one definition for the empty case, which is the identity. We have one definition for the head/tail case.

### Let's generalize the recursive patterns

Let's use a value `z` for the empty case. Let's use a function `f` for the head/tail case that combines the head with the result of the `recursive call` on the tail

```Haskell
-- Find the sum of all elements in a list
sum :: [Integer] -> Integer
sum []     = 0 -- the value 0 here can be represented by `z`
sum (x:xs) = (+) x (sum xs) -- the function `(+)` can be represented by `f`

-- Find the length (or number of the elements) of a list
length :: [a] -> Integer
length []     = 0 -- the value 0 here can be represented by `z`
length (_:xs) = (+) 1 (length xs) -- the function `(+)` can be represented by `f`

-- Find the product of all elements in a list
product :: [Integer] -> Integer
product []     = 1 -- the value 1 here can be represented by `z`
product (x:xs) = (*) x (product xs) -- the function `(*)` can be represented by `f`

-- concatenate the elements of a list
concat :: [[a]] -> [a]
concat []     = []  -- the value [] here can be represented by `z`
concat (x:xs) = (++) x (concat xs) -- the function `(++)` can be represented by `f`

-- convert to string all elements of a list
toString :: [Int] -> String
toString [] = "" -- the value "" here can be represented by `z`
toString (x:xs) = ((++).show) x (toString xs) -- the function `((++).show)` can be represented by `f`

-- Does all the elements in a list satisfy a property?
all :: (a -> Bool) -> [a] -> Bool
all _ [] = True  -- the value True here can be represented by `z`
all p (x:xs) = ((&&).p) x (all p xs) -- the function `((&&).p)` can be represented by `f`
```

We can generalize the structural pattern above into `foldr`.

```Haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f z [] = z
foldr f z (x:xs) = f x (foldr f z xs)

```

## 10.4 Fold right

We call `foldr` the "right fold". `foldr` is right associative, that is, it associates to the right. Folding happens in 2 stages, traversal and folding. Traversal is the stage in which the fold recurses over the spine. Folding refers to the evaluation or reduction of the folding function applied to the values.

```Haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f z []     = z
foldr f z (x:xs) = f x (foldr f z xs)
```

All folds recurse over the spine in the same direction; the difference between left folds and right folds is in the association or parenthesization of the folding function and, thus, which direction the folding or reduction proceeds. Given this two-stage process and non-strict evaluation, if `f` does not evaluate its second argument (`foldr f z xs`), no more of the spine will be forced, hence, `foldr` can be used with lists that are potentially infinite. Another term for never-ending evaluation is *bottom* or *undefined*. There is no guarantee that a fold of an infinite list will finish evaluating even if we used `foldr`, it depends on the input data and the fold function. The first argument `x` in `f x (foldr f z xs)` involves a pattern match that is strict by default, `foldr` must force an initial cons cell in order to discriminate between the `[]` and the `(x:xs)` cases, so the first cons cell cannot be undefined.

## 10.5 Fold left

Left folds traverse the spine in the same direction as right folds, but their folding process is left associative and proceeds in the opposite direction as that of `foldr`. `foldl` is left associative.

```Haskell
foldl :: (b -> a -> b) -> b -> [a] -> b
foldl f acc []      = acc
foldl f acc (x:xs)  = foldl f (f acc x) xs
```

Use the trick below to see the associativity of `foldr` or `foldl`.

```haskell
Prelude> let f = (\x y -> concat ["(", x, "+", y, ")"])
Prelude> foldl f "0" (map show [1..5])
"(((((0+1)+2)+3)+4)+5)"
```

`foldl` begins its reduction process by adding the `acc` (accumulator) value to the head of the list, whereas `foldr` had added it to the final element of the list first. If your folding function is not commutative, a left fold can lead to a different result than a right fold of the same.

We can use `scanr` or `scanl` to see how folds evaluate. Scans are similar to folds but return a list of all the intermediate stages of the fold. The relationship between the scans and folds are as follows:

```haskell
last (scanl f z xs) = foldl f z xs
head (scanr f z xs) = foldr f z xs
```

An importance difference between `foldr` and `foldl` is that a left fold has the successive steps of the fold as its first argument. `foldl` unconditionally evaluates the spine. `foldl` is generally inappropriate with lists that are or could be infinite. Combination of the forced evaluation of the spine by `foldl` with non-strictness affects performance negatively when dealing with a long list.

In most cases, when you need a left fold, use `foldl'` (fold-l-prime), which works the same as `foldl` except that it is strict. It forces evaluation of the values inside the cons cells as it traverses the spine instead of accumulating unevaluated expressions for each element of the list.

## 10.6 How to write fold functions

To write a fold, we first think about what our start value for the folding function should be. The start value is typically the identity function for the folding function. Next, we think about the arguments. The folding function takes two arguments, `a` and `b`. `a` is an element in the list we are applying the folding function to and `b` is either the start value or the value accumulated as the list is being processed.

## 10.7 Folding and evaluation

What differentiates `foldr` and `foldl` is associativity. `foldr` evaluates from the innermost cons cell to the outermost (the head). `foldl` recurses unconditionally to the end of the list through self-calls and then the folding function evaluates from the outermost cons cell to the innermost. The relationship between `foldr` and `foldl` only for finite lists is:

`foldr f z xs = foldl (flip f) z (reverse xs)`

## 10.8 Summary

### foldr

```Haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f z []     = z
foldr f z (x:xs) = f x (foldr f z xs)
```

The rest of the fold (`foldr f z xs`) is an argument to the folding function `f` you passed to `foldr`. So, it does not self-call as a tail-call like `foldl`. `foldr` associates to the right and works with infinite Lists. `foldr` is a good default choice whenever we want to transform data structures.

### foldl

```Haskell
foldl :: (a -> b -> b) -> b -> [a] -> b
foldl f z []     = z
foldl f z (x:xs) = foldl f (f z x) xs
```

It Self-calls (tail-calls) through the list, only beginning to produce values after it has reached the end of the list. `foldl` associates to the left and cannot be used with infinite lists. It is nearly useless and should almost always be replaced with `foldl'`.

## 10.9 Scans

Scans accumulate values like folds, but return a list of the accumulated results. Scans are not catamorphisms.

## 10.11 Definitions

1. A *Fold* is a higher-order function which, given a function to accumulate the results and a recursive data structure, returns the built up value.
2. A *catamorphism* is a generalization of folds to arbitrary datatypes.
3. A *tail call* is the final result of a function.
4. *Tail recursion* is a function whose tail calls are recursive invocations of itself.

## References

1. [Haskell Programming from First Principles](http://haskellbook.com/)
2. [All About a Fold](https://github.com/lambdaconf/lambdaconf-2016-usa/blob/master/All%20about%20a%20Fold/All-about-a-fold.pdf)
3. [Folds and Infinite Lists](http://argumatronic.com/posts/2016-09-17-infinite-folds.html)
4. [The Magic of Folds](http://www.parsonsmatt.org/2016/03/24/the_magic_of_folds.html)
5. [foldr vs foldl in haskell](http://davidkblackmon.com/foldr-in-haskell/)
